(define sxslt-id       (lambda args args))
(define sxslt-flatten  (lambda (tag . args) args))
(define sxslt-drop     (lambda args '()))

(define (db tagname)
  (string->symbol (string-append "http://docbook.org/ns/docbook:" tagname)))

(define (direct-map-inline xml-tag tex-command)
  (cons (db xml-tag)
        (lambda (tag . rest) `(cmd ,tex-command (wr nonl2) (gr ,@rest)))))
(define (sxslt-unknown-tag tag . rest)
  (display (string-append "*** Unprocessed tag: "
                          (symbol->string tag) "\n") (current-error-port))
  `(cmd "TODO" (gr ,(symbol->string tag))))

(define (common-transform doc)
  (define inquote? #f) ; #f=level0,2,4... #t=level1,3,5...
  (define conv-map `(
      (*default* *preorder* . ,sxslt-unknown-tag)
      (*PI* *preorder* . ,sxslt-drop)
      (*text*          . ,sxslt-flatten)
      (@    *preorder* . ,sxslt-drop)
      (,(db "chapter") (
          (,(db "title") . ,(lambda (tag . rest) `(cmd "chapter" (gr ,@rest))))
          )                . ,sxslt-flatten)
      (,(db "section") (
          (,(db "title") . ,(lambda (tag . rest) `(cmd "section" (gr ,@rest))))
          )                . ,sxslt-flatten)
      (,(db "info")        . ,sxslt-flatten)
      (,(db "pubdate")     . ,sxslt-drop)
      (,(db "releaseinfo") . ,sxslt-drop)
      (,(db "para")        . ,(lambda (tag . rest) `(env "para" (wr nonl2 nonl3) ,@rest)))
      (,(db "programlisting")       . ,(lambda (tag . rest) `(env "programlisting" (wr nonl2 nonl3) ,@rest)))
      (,(db "indexterm") *preorder* . ,sxslt-drop)
      ,(direct-map-inline "tag"       "tag")
      ,(direct-map-inline "acronym"   "acronym")
      ,(direct-map-inline "firstterm" "firstterm")
      ,(direct-map-inline "command"   "command")
      ,(direct-map-inline "uri"       "uri")
      (,(db "phrase")        . ,sxslt-flatten)
      (,(db "xref") *preorder*  . ,(lambda args "(TODO-xref)"))
      (*TOP* . ,(lambda (tag . rest)
          `(texml
             (cmd "documentclass" (gr "book"))
             (cmd "usepackage" (gr "texml"))
             (env "document"
                  ,@rest))))
      (,(db "quote") *preorder* . ,(lambda (tag . rest)
           (set! inquote? (not inquote?))
           (let ((in (pre-post-order rest conv-map))
                 (bq `(lang ,(if inquote? "startquote" "nestedstartquote")))
                 (eq `(lang ,(if inquote? "endquote" "nestedendquote"))))
             (set! inquote? (not inquote?))
             (list bq in eq))))
      ))
  (pre-post-order doc conv-map)
  )
